buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net/' }
		maven { url = 'https://repo.spongepowered.org/maven' }
		mavenCentral()
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.+'
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = '1.0'
group = 'com.github.singlerr'
archivesBaseName = 'strontium'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {

	mappings channel: 'stable', version: '39-1.12'

	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			workingDirectory project.file('run')

			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

			property 'forge.logging.console.level', 'debug'
			arg '-torg.spongepowered.asm.launch.MixinTweaker'
		}

		server {

			property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

			property 'forge.logging.console.level', 'debug'
		}
	}
}

repositories {
	mavenCentral()
	maven { url = 'https://repo.spongepowered.org/maven' }
}

configurations {
	embed
	implementation.extendsFrom(embed)
}

mixin {
	add sourceSets.main, 'rubidium.refmap.json'
	config 'rubidium.mixins.json'
	config 'rubidium.compat.mixins.json'
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

	implementation 'org.slf4j:slf4j-api:1.7.25'

	implementation 'org.lwjgl:lwjgl:3.2.2'

	implementation 'org.jetbrains:annotations:24.0.1'
	annotationProcessor 'org.spongepowered:mixin:0.8.3-SNAPSHOT:processor'
	embed('org.spongepowered:mixin:0.8.3-SNAPSHOT') {
		exclude module: 'guava'
		exclude module: 'commons-io'
		exclude module: 'gson'
	}
}

jar {
	from {
		configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
	} {
		exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
	}
	manifest {
		attributes([
				"Specification-Title": "Strontium",
				"Specification-Vendor": "singlerr",
				"Specification-Version": "1",
				"Implementation-Title": project.name,
				"Implementation-Version": "${version}",
				"Implementation-Vendor" :"singlerr",
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

jar.finalizedBy('reobfJar')

